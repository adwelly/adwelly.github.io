---
layout: post
title: Hydra
---

 Hydra is an open source Clojure library to help create and manipulate deeply nested data structures. I've given a couple of talks about it, one of which was videoed [here](https://skillsmatter.com/skillscasts/9158-deeply-nested-data-structures-are-we-sucking-our-eggs-wrong) (it's about 15 minutes long and you have to register on the site).

The usual caveats apply. First of all, there are several other approaches and I personly recommend both [Lenses](http://blog.podsnap.com/pinhole.html) and Nathan Mertz's [Specter](https://github.com/nathanmarz/specter) . Secondly, although Hydra is rapidly approaching alpha, it is still subject to change. That said, I'm only expecting to add things from this point forward, so I think you can safely rely on the fact that code using it will not break in the future, although the future may offer better ways of doing things.

The problem that Hydra seeks to solve is the difficulty we face when manipulating deeply nested data structures. The first step is to acknowledge that there is an actual problem working with â€” what we may as well call trees. This is an  area where functional programs have to work a bit harder than imperative programs. In the imperative world, you traverse the data structure to the point where a change needs to be made, and do the modification in place. In the functional world, the data structures are immutable, so we traverse the original structure to the point of interest, use a function to create the new data, and then we have to recreate the rest of the original structure around the new information.

Hydra takes  an  alternative approach to both Lenses and Specter. It treats trees as a set of paths where each path maps onto a single value. I've called these structures 'path maps' (I was up all night thinking every that name). Broadly speaking, we can convert any tree consisting of maps, vectors, and sets into a the path map. This can be manipulated with a small set of operators, and then the the path map can be reconstituted back into its standard form.

To give an example that I've pretty much lifted from Specter,  consider a data structure representing a bank where individuals with more than 100, 000 in their accounts will pay a bank fee of 1000.

Here is the structure:

```clojure
{:people
       [{:money 128825 :name "Alice Brown"}
        {:money 100 :name "John Smith"}
        {:money 499999000 :name "Scrooge McDuck"}
        {:money 2870 :name "Charlie Johnson"}
        {:money 8272280 :name "Michael Smith"}]
 :bank {:funds 470000003000}}
```

The standard Clojure approach would be something like:

```clojure
(defn- take-fee-from-big-account [{:keys [money] :as ac}]
  (if (< 100000 money)
    (assoc ac :money (- money 1000))
    ac))

(defn- calculate-fees [total-fees {money :money}]
  (if (< 100000 money) (+ total-fees 1000) total-fees))

(defn conventional [world]
  (let [the-people (world :people)
      updated-accounts (mapv take-fee-from-big-account the-people)
      total-fees (reduce calculate-fees 0 the-people)]
  {:people updated-accounts :bank {:funds (+ (get-in world [:bank :funds]) total-fees)}}))
```

But the problem here is that the 'conventional' function has to make two passes over the data structure. The first pass calculates the new account values, the second, the total fees that have been gathered. This is inefficient and of course, the bank actually has millions of accounts.

To do this in a single pass you can write this:

```clojure
(defn single-pass-take-fees [{:keys [money] :as ac}]
  (if (< 100000 money)
   (list (assoc ac :money (- money 1000)) 1000)
   (list ac 0)))

(defn single-pass [world]
  (let [the-people (world :people)
            updated-accounts-and-fees (map single-pass-take-fees the-people) ;(a)
            updated-accounts (mapv first updated-accounts-and-fees)
            total-fee-list (map second updated-accounts-and-fees)
            total-fees (apply + total-fee-list)]
        {:people updated-accounts :bank {:funds (+ (get-in world [:bank :funds]) total-fees)}}))

```

Now the problem of complexity should be clearer. In order to do this in one pass, you have to create an intermediate data structure, a vector of tuples  (account values,fees) on line 'a'. Clojure can create this extra data structure efficiently, but nevertheless, it had to be made. Such arbitrary intermediate structures are extremely common and they clutter up the design. They pose a real difficulty in understanding what the code is doing. It should also be clear that this kind of thing is utterly trivial in a language like python or java.

The Hydra approach is:

```clojure
(defn- accounts-with-more-than-100000? [path v]
  (and (starts-with? [:people] path)
       (ends-with? [:money] path)
       (<= 100000 v)))

(defn hydra [world]
  (let [world-paths (to-path-map world)
      big-accounts (pmfilter world-paths accounts-with-more-than-100000?) ; (a)
      taxed-accounts (vmap big-accounts #(- % 1000))                      ; (b)
      total-collected (* 1000 (count big-accounts))]                      ; (c)
  (from-path-map (upsert world-paths taxed-accounts {[:bank :funds] (+ total-collected (world-paths [:bank :funds]))}))))
```

In line 'a', the complete sort of paths are filtered to return the acounts of interest. In line 'b', we create a map with the same paths but with the values reduced by the amount of the fee. In line 'c' we calculate the total fees. Although it only needs a single pass, the of it looks very similar to the original Clojure code.

Hydra will be going into Clojars as soon as it reaches alpha, and then I will be using it on

