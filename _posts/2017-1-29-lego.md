---
layout: post
title: Modelling toy bricks
---

[The Man Upstairs](http://m.imdb.com/name/nm0002071): You know the rules, this isn't a toy!

[Finn](http://m.imdb.com/name/nm3338018): Um... it kind of is.

[The Man Upstairs](http://m.imdb.com/name/nm0002071): No, actually it's a highly sophisticated inter-locking brick system.

 Of course, as everyone knows, it isn't a toy! Also, it turns out that you can model bricks in Clojure, more or less. That's probably less well known. WARNING - until I've printed some examples and tried them, I'm not claiming Lego(tm) compatability — watch this space.

The key to this is a piece of Open Source software called [OpenSCAD](http://www.openscad.org), and in my case, both the inspiration of [Matt Aderath](https://www.youtube.com/watch?v=uk3A41U0iO4&t=1s) and a mild dose of flu that kept me off work for a couple of days this week. I played around with the library mentioned in the video and in typical dev fashion decided that I didn't like it and would rate my own (I was feelling grumpy — did I mention that I had the flu?).

OpenSCAD uses constructive solid geometry, a lot of big words for a very simple idea. Take a shape like a cube:

![cube](https://adwelly.github.io/images/cube.png)

And then you can add another shape, like a cylinder:

![cubepluscyl](https://adwelly.github.io/images/cubepluscyl.png)

Or subtract it:

![cubeminuscyl](https://adwelly.github.io/images/cubeminuscyl.png)

Naturally, you can move shapes around (this one is three cylinders in the three planes):

![jackstone](https://adwelly.github.io/images/jackstone.png)

Or stretch them:

![stretch](https://adwelly.github.io/images/stretch.png)

You can also fit a hull, which is a fancy way of  draping a skin around an existing collection of shapes:

![hull](https://adwelly.github.io/images/hull.png)

That's pretty much the entire idea, although it must be admitted that it takes some rather clever math to actually make this work. Fortunately the hard work has already been done by OpenSCAD, and all I have to do is produce scad files that describe arbitrary shapes in the these kind of terms.

Scad files have a c-like syntax, and they can do a lot, but they have some quirks. I much prefer to work with the abstraction and function composition tools provided by Clojure and in any case I don't usually need an excuse to fire up my repl. The first step is to create a data structure that represents the primitive objects that are needed. I used maps to do this:

```clojure
{:primitive :cylinder :r 10 :h 55}
```

or

```clojure
{:primitive :cube :x 20 :y 20 :z 20}
```

Each map has to be translated into a string of scad and that could quite easily be accomplished with an extended cond, but I prefer Clojure's built in multi-method for this kind of thing. The first line in this code states that the primitive-gen function will apply :primitive to  each incoming map and use the result to work out which version of the method to execute.

```clojure
(defmulti primitive-gen :primitive) ;; Line a

(defmethod primitive-gen :sphere [{:keys [r center trans]}]
  ...
  
(defmethod primitive-gen :cube [{:keys [size center trans] :as shape}]
  ....
  
(defmethod primitive-gen :cylinder [{:keys [r r1 r2 h center fa fs fn trans] :as shape}]
  ....
```

When I started, each primitive created its own string, but it was soon obvious that there was so much shared commonality that a separate function to actually generate the string and handle the comma seperated parameters and brackets was an improvement. So I ended up with this code pretty quickly:

```clojure
(defn func-to-str [{:keys [name params vec] :as p}]
  (let [pms (for [[k v] (dissoc params :trans)] (str (name-of k) "=" (val-of v)))
        param-str (s/join "," pms)
        vc (when vec (val-of vec))]
    (str (if (:trans params) (str "#" name) name) "(" (if vc vc "") param-str ")")))
```

I have a :trans parameter that if set to true makes the shape translucent - handy for understanding subtraction. Unfortunately, scad files represent this as a special # character at the beginning of the shape name so it has to be handled specially

The operators union, translate, scale, and hull work in a similar fashion, but since they have to work over a collection of shapes, the code generation has to operate recursively with braces around the sub shapes. I could have put something in to format the resulting scad output nicely, but I wasn't planning to spend a lot of time reading it. That was the correct call as it turned out.

```clojure
(defn code-gen [shape]
  (cond (contains? shape :primitive) (-> shape primitive-gen func-to-str (str ";\n"))
        (contains? shape :operator) (let [sub-shapes (map code-gen (:content shape))
                                          sub-str (s/join sub-shapes)]
                                      (str (-> shape operator-gen func-to-str) "{\n" sub-str "}\n"))
```

The complete piece of code took about three hours to put together. 

The following day I was feeling a bit better so I decided to have a go at some parametric lego-like structures. First of all I needed a better way to create operators and primitives, so I whistled up a small set of convenience functions:

```clojure
(defn cube [x y z]
  {:primitive :cube
   :size      [x y z]})

(defn cylinder [h r]
  {:primitive :cylinder
   :h         h
   :r         r})

(defn difference [a b & shapes]
  {:operator :difference
   :content  (concat [a b] shapes)})

(defn translate [v & shapes]
   {:operator :translate
    :vec      v
    :content  (vec shapes)})

(defn union
  ([a] {:operator :union :content (if (seq? a) a [a])})
  ([a b & shapes]
   {:operator :union
    :content  (concat [a b] shapes)}))
```

Now I could easily create a simple one stud brick:

```clojure
(def brick-dim 7.8)
(def stud-height 1.77)
(def stud-radius 2.45)

(defn studded-cube [l w h stud-h stud-r]
  {:operator :color
   :vec      [1 0 0]
   :content  [(union (cube l w h)
                     (translate [0 0 half-stud-height] (cylinder (+ h stud-h) stud-r)))]})

(defn stud1 [h]
  (studded-cube brick-dim brick-dim h stud-height stud-radius))
```

Scad numbers are always in mm, and I have a cheap set of digital calipers so I was able to find out more or less exactly how big a one stud brick actually is:

![stud1](https://adwelly.github.io/images/stud1.png)Now it's possible to write out function for a brick with an arbitrary number of studs in x and y:

```clojure
(defn solid [h m n]
  (let [row (for [i (range m)] (translate [(* i brick-dim) 0 0] (stud1 h)))
        cols (for [j (range n)] (translate [0 (* j brick-dim) 0] (union row)))]
    (union cols)))
```

Which produces:

![3x4](https://adwelly.github.io/images/3x4.png)A rather complicated function produces an interior structure for the brick (not quite realistic at the moment).

![interior](https://adwelly.github.io/images/interior.png)

And this can then dropped into a hollowed out brick:

![brick](https://adwelly.github.io/images/brick.png)

This needs some more work but when that's done I'm going to try and get this 3d printed and see if it actually fits. Until then — caveat emptor.

You can find this code, with a few more details, at my [Blox](https://github.com/adwelly/blox) repository on github.